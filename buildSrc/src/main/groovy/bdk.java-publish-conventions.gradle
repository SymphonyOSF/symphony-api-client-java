plugins {
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            pom(rootProject.ext.pomDefinition)
            pom.withXml {
                // otherwise project description is evaluated too early
                asNode().children().first().plus {
                    setResolveStrategy(Closure.DELEGATE_FIRST)
                    'name' project.name
                    'description' project.description
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username rootProject.ext.symphonyRepoUser
                password rootProject.ext.symphonyRepoPassword
            }
            url rootProject.ext.symphonyRepoUrl
        }
    }
}

signing {
    required { rootProject.isReleaseVersion }
    sign publishing.publications.maven
}
